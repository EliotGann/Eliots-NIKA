#pragma rtGlobals=1		// Use modern global access method.


Function EGN_MakeSectorGraph()

	string OdlDf=GetDataFolder(1)
	SetDataFolder root:Packages:Convert2Dto1D
	NVAR SectorsNumSect=root:Packages:Convert2Dto1D:SectorsNumSect
	NVAR SectorsSectWidth=root:Packages:Convert2Dto1D:SectorsSectWidth
	NVAR SectorsGraphEndAngle= root:Packages:Convert2Dto1D:SectorsGraphEndAngle
	NVAR SectorsGraphStartAngle= root:Packages:Convert2Dto1D:SectorsGraphStartAngle
	NVAR ImageDisplayLogScaled=root:Packages:Convert2Dto1D:ImageDisplayLogScaled
	nvar wavelength=root:Packages:Convert2Dto1D:wavelength
	string wavelengths = num2str(round(wavelength*100))
	NVAR A2DLineoutDisplayLogInt=root:Packages:Convert2Dto1D:A2DLineoutDisplayLogInt
	A2DLineoutDisplayLogInt=ImageDisplayLogScaled				//set to same scaling as user has for the image file
	NVAR SectorsUseRAWData=root:Packages:Convert2Dto1D:SectorsUseRAWData
	NVAR SectorsUseCorrData=root:Packages:Convert2Dto1D:SectorsUseCorrData
	if(SectorsUseCorrData)
		EGNA_CorrectDataPerUserReq("",wavelengths)								//calibrate data
	endif
	EGN_MakeSqMatrixOfLineouts(SectorsNumSect,SectorsSectWidth,SectorsGraphStartAngle,SectorsGraphEndAngle)		//convert to lineout
	
	wave SquareMap=root:Packages:Convert2Dto1D:SquareMap
	//duplicate/O SquareMap, SquareMap_dis
	NVAR A2DImageRangeMinLimit=root:Packages:Convert2Dto1D:A2DImageRangeMinLimit
	NVAR A2DImageRangeMaxLimit=root:Packages:Convert2Dto1D:A2DImageRangeMaxLimit
	NVAR A2DImageRangeMin=root:Packages:Convert2Dto1D:A2DImageRangeMin
	NVAR A2DImageRangeMax=root:Packages:Convert2Dto1D:A2DImageRangeMax
	
	if(A2DLineoutDisplayLogInt)
		MatrixOP/O SquareMap_dis=log(SquareMap)
	else
		MatrixOP/O SquareMap_dis=SquareMap
	endif
	wavestats/Q   SquareMap_dis
	A2DImageRangeMinLimit=V_min
	A2DImageRangeMin=V_min
	A2DImageRangeMaxLimit=V_max
	A2DImageRangeMax=V_max
	
	DoWindow SquareMapIntvsPixels
	if(!V_Flag)
		Execute("EGN_SquareGraph()")
	else
		DoWindow/F SquareMapIntvsPixels
	endif

end

Function EGN_MakeSqMatrixOfLineouts(SectorsNumSect,AngleWidth,SectorsGraphStartAngle,SectorsGraphEndAngle)
	variable SectorsNumSect,AngleWidth,SectorsGraphStartAngle,SectorsGraphEndAngle
	//Create matrix of lineouts using the ImageLineProfile function
	//will have to be finished, for now it is simple method... 
	string OdlDf=GetDataFolder(1)
	SetDataFolder root:Packages:Convert2Dto1D
	variable AngleStep = (SectorsGraphEndAngle-SectorsGraphStartAngle)/SectorsNumSect
	
	NVAR SectorsUseRAWData=root:Packages:Convert2Dto1D:SectorsUseRAWData
	NVAR SectorsUseCorrData=root:Packages:Convert2Dto1D:SectorsUseCorrData
	if(SectorsUseRAWData)
		Wave CCDImageToConvert=root:Packages:Convert2Dto1D:CCDImageToConvert
	else
		Wave CCDImageToConvert=root:Packages:Convert2Dto1D:Calibrated2DDataSet
	endif
	string OriginalNote=note(CCDImageToConvert)
	string NewNote, MaskSquareImageNote
	Wave Mask=root:Packages:Convert2Dto1D:M_ROIMask
	Wave/Z MaskSquareImage
	if(WaveExists(MaskSquareImage))
		MaskSquareImageNote=note(MaskSquareImage)
	else
		MaskSquareImageNote=""
	endif
	NVAR BeamCenterX=root:Packages:Convert2Dto1D:BeamCenterX
	NVAR BeamCenterY=root:Packages:Convert2Dto1D:BeamCenterY
	NVAR A2DmaskImage=root:Packages:Convert2Dto1D:A2DmaskImage
	SVAR CurrentMaskFileName=root:Packages:Convert2Dto1D:CurrentMaskFileName
	NewNote = OriginalNote
	NewNote+="BeamCenterX="+num2str(BeamCenterX)+";"
	NewNote+="BeamCenterY="+num2str(BeamCenterY)+";"
	NewNote+="CurrentMaskFileName="+CurrentMaskFileName+";"
	NewNote+="SectorsNumSect="+num2str(SectorsNumSect)+";"
	NewNote+="AngleWidth="+num2str(AngleWidth)+";"
	NewNote+="SectorsGraphStartAngle="+num2str(SectorsGraphStartAngle)+";"
	NewNote+="SectorsGraphEndAngle="+num2str(SectorsGraphEndAngle)+";"
	//for now work in pixles...
	//find maximum distance from center to corners
	variable dist00=sqrt(BeamCenterX^2 + BeamCenterY^2)
	variable dist0Max = sqrt(BeamCenterX^2 + (BeamCenterY - dimSize(CCDImageToConvert,1)) ^2) 
	variable distMax0 = sqrt((BeamCenterX - dimSize(CCDImageToConvert,0))^2 + BeamCenterY ^2) 
	variable distMaxMax = sqrt((BeamCenterX - dimSize(CCDImageToConvert,0))^2 + (BeamCenterY - dimSize(CCDImageToConvert,1)) ^2) 
	variable MaxDist = floor(max(max(dist00,dist0Max),max(distMax0,distMaxMax ))	)	//max number of pixles from the beam center to end
	
	variable RecalculateMask=0
	if(A2DmaskImage)
		variable oldBeamCenterX=NumberByKey("BeamCenterX",MaskSquareImageNote,"=")
		variable oldBeamCenterY=NumberByKey("BeamCenterY",MaskSquareImageNote,"=")
		string oldCurrentMaskFileName=StringByKey("CurrentMaskFileName",MaskSquareImageNote,"=")
		variable oldSectorsNumSect=NumberByKey("SectorsNumSect",MaskSquareImageNote,"=")
		variable oldAngleWidth=NumberByKey("AngleWidth",MaskSquareImageNote,"=")
		variable oldSectorsGraphStartAngle=NumberByKey("SectorsGraphStartAngle",MaskSquareImageNote,"=")
		variable oldSectorsGraphEndAngle=NumberByKey("SectorsGraphEndAngle",MaskSquareImageNote,"=")
		
		variable diff1 = (oldBeamCenterX!=BeamCenterX || oldBeamCenterY!=BeamCenterY || cmpstr(oldCurrentMaskFileName,CurrentMaskFileName)!=0)
		variable diff2 = (oldSectorsNumSect!=SectorsNumSect || oldAngleWidth!=AngleWidth || oldSectorsGraphStartAngle!=SectorsGraphStartAngle || oldSectorsGraphEndAngle!=SectorsGraphEndAngle) 
		if( diff1 || diff2 ||1)
			RecalculateMask=1
			print "recalculate Square Mask also"
			Duplicate/O Mask, MaskS
			Redimension/S MaskS
			Make/O/N=(MaxDist,SectorsNumSect) MaskSquareImage
		endif
	endif
	Duplicate/O CCDImageToConvert, MaskedImage	//working waves
	Redimension/S MaskedImage					//to use NaN as masked point, this has to be single precision
	Make/O/N=(MaxDist,SectorsNumSect) SquareMap			//create angle vs point number squared intensity wave
	Make/O/N=(MaxDist) PixelAddressesX, PixelAddressesY, PathWidth, PathWidthTemp	//create addresses and width for path around which to get profile 
	PathWidth = 2* p * tan(AngleWidth*(pi/180))		//create the path profile width - same for all sectors

	variable ang, indx, i
	variable NumPntsX, NumPntsY, tempVal
	indx = SectorsNumSect
	ang = SectorsGraphStartAngle
	For(i=0;i<SectorsNumSect;i+=1)			//evaluate the sectors
		Redimension/N=(MaxDist) PathWidthTemp, PixelAddressesY, PixelAddressesX
		PixelAddressesX=BeamCenterX + p * cos((SectorsGraphStartAngle+(i*AngleStep))*(pi/180))		//calculate the path, this is now in "pixles", assumes same
		PixelAddressesY=BeamCenterY- p * sin((SectorsGraphStartAngle+(i*AngleStep))*(pi/180))		// pixel size in both directions
		//now need to check for indexes outside the image, so we do not needlessly calculate poitns outside..
		NumPntsX=MaxDist
		NumPntsY=MaxDist
		wavestats/Q PixelAddressesX
		if(V_min<0)			//min on wave less than 0, crosses 0
			NumPntsX = BinarySearch(PixelAddressesX,0)
		endif
		if(V_max>dimSize(CCDImageToConvert,0))
			NumPntsX=BinarySearch(PixelAddressesX,dimSize(CCDImageToConvert,0))
		endif
		wavestats/Q PixelAddressesY
		if(V_min<0)
			NumPntsY = BinarySearch(PixelAddressesY,0)
		endif
		if(V_max>dimSize(CCDImageToConvert,1))
			NumPntsY=BinarySearch(PixelAddressesY,dimSize(CCDImageToConvert,1))
		endif
		tempVal = min(NumPntsX,NumPntsY)
		Redimension/N=(tempVal) PathWidthTemp, PixelAddressesY, PixelAddressesX
		PathWidthTemp = PathWidth
		//and now the data should be all calcualte only within the image....
		
//		//SCALE X POSITIONS //Eliot adding this
		duplicate /o PixelAddressesY, PixelAddressesYscaled
		duplicate /o PixelAddressesX, PixelAddressesXscaled
		PixelAddressesYscaled = DimOffset(MaskedImage, 1) + PixelAddressesY[p] *DimDelta(MaskedImage,1)
		PixelAddressesXscaled = DimOffset(MaskedImage, 0) + PixelAddressesX[p] *DimDelta(MaskedImage,0)
//		//END change by Eliot
		
		ImageLineProfile xWave=PixelAddressesXscaled, yWave=PixelAddressesYscaled, srcwave=MaskedImage , widthWave=PathWidthTemp
	//	ImageLineProfile xWave=PixelAddressesX, yWave=PixelAddressesY, srcwave=MaskedImage , width=2
		Wave W_ImageLineProfile
		Redimension /N=(MaxDist) W_ImageLineProfile
		W_ImageLineProfile[tempVal,inf ] = NaN
		SquareMap[][i] = W_ImageLineProfile[p]
		if(recalculateMask)//TEMP
			duplicate /o PixelAddressesY, PixelAddressesYscaled
			duplicate /o PixelAddressesX, PixelAddressesXscaled
			PixelAddressesYscaled = DimOffset(MaskS, 1) + PixelAddressesY[p] *DimDelta(MaskS,1)
			PixelAddressesXscaled = DimOffset(MaskS, 0) + PixelAddressesX[p] *DimDelta(MaskS,0)
			ImageLineProfile xWave=PixelAddressesXscaled, yWave=PixelAddressesYscaled, srcwave=MaskS , widthWave=PathWidthTemp
			Wave W_ImageLineProfile
			W_ImageLineProfile = W_ImageLineProfile[p]>0.9999 ? W_ImageLineProfile[p] : NaN
 			Redimension /N=(MaxDist) W_ImageLineProfile
			W_ImageLineProfile[tempVal,inf ] = NaN
			
			MaskSquareImage[][i] = W_ImageLineProfile[p]
		endif
	endfor	
	Note SquareMap, NewNote
	if(recalculateMask)
		Note MaskSquareImage, NewNote
	endif
	if(A2DmaskImage)
		MatrixOP/O SquareMap=SquareMap*(MaskSquareImage/MaskSquareImage)
	endif
	SetScale/P y SectorsGraphStartAngle,AngleStep,"", SquareMap
	KillWaves/Z MaskedImage
	KillWaves/Z MaskS
	DoWindow SquareMapIntvsPixels
	if(V_Flag)
		DoWindow/K SquareMapIntvsPixels
	endif
	//comment
	// In order to convert the data next into Int vs Q scale, we need to produce also Q scale which would map pixels into Q, this is 
	//function of geometry...
	// also we need to propage somehow errors through. This can be done here, but it is unclear to me how to easily propaget it further.
	
end



Function EGN_SquareGraph() : Graph

	Wave SquareMap_dis=root:Packages:Convert2Dto1D:SquareMap_dis
	NVAR A2DImageRangeMinLimit=root:Packages:Convert2Dto1D:A2DImageRangeMinLimit
	NVAR A2DImageRangeMaxLimit=root:Packages:Convert2Dto1D:A2DImageRangeMaxLimit
	PauseUpdate; Silent 1		// building window...
	Display /W=(191.25,169.25,705,562.25)/K=1; AppendImage SquareMap_dis
	DoWindow/C/T SquareMapIntvsPixels,"SquareMap of intensity vs pixel"
	ControlBar 40
	CheckBox DisplayLogLineout,pos={10,8},size={90,14},proc=EGNA_SquareCheckProc,title="Log Int?"
	CheckBox DisplayLogLineout,help={"Display 2D map oflineouts in log units?"}
	CheckBox DisplayLogLineout,variable= root:Packages:Convert2Dto1D:A2DLineoutDisplayLogInt
	Slider ImageRangeMinSquare,pos={100,4},size={150,16},proc=EGNA_MainSliderProc,variable= root:Packages:Convert2Dto1D:A2DImageRangeMin,live= 0,side= 3,vert= 0,ticks= 0
	Slider ImageRangeMinSquare,limits={A2DImageRangeMinLimit,A2DImageRangeMaxLimit,0}
	Slider ImageRangeMaxSquare,pos={100,20},size={150,16},proc=EGNA_MainSliderProc,variable= root:Packages:Convert2Dto1D:A2DImageRangeMax,live= 0,side= 3,vert= 0,ticks= 0
	Slider ImageRangeMaxSquare,limits={A2DImageRangeMinLimit,A2DImageRangeMaxLimit,0}
//
	ModifyImage SquareMap_dis ctab= {*,*,Terrain,0}
	ModifyGraph margin(left)=38,margin(bottom)=25,margin(top)=14,margin(right)=14
	ModifyGraph mirror=2
	ModifyGraph nticks(left)=10
	ModifyGraph minor=1
	ModifyGraph fSize=8
	ModifyGraph standoff=0
	ModifyGraph tkLblRot(left)=90
	ModifyGraph btLen=3
	ModifyGraph tlOffset=-2
//	ModifyGraph swapXY=1
	ModifyGraph mirror(left)=1
	Label bottom "Pixels from beam center"
	Label left "Azimuthal angle [degrees]"
EndMacro

Function EGNA_SquareCheckProc(ctrlName,checked) : CheckBoxControl
	String ctrlName
	Variable checked

	if(cmpstr("DisplayLogLineout",ctrlName)==0)
		Wave SquareMap_dis=root:Packages:Convert2Dto1D:SquareMap_dis
		Wave SquareMap=root:Packages:Convert2Dto1D:SquareMap
		NVAR A2DLineoutDisplayLogInt=root:Packages:Convert2Dto1D:A2DLineoutDisplayLogInt
	
		if(A2DLineoutDisplayLogInt)
			SquareMap_dis=log(SquareMap)
		else
			SquareMap_dis=SquareMap
		endif
		NVAR A2DImageRangeMinLimit=root:Packages:Convert2Dto1D:A2DImageRangeMinLimit
		NVAR A2DImageRangeMaxLimit=root:Packages:Convert2Dto1D:A2DImageRangeMaxLimit
		NVAR A2DImageRangeMin=root:Packages:Convert2Dto1D:A2DImageRangeMin
		NVAR A2DImageRangeMax=root:Packages:Convert2Dto1D:A2DImageRangeMax
		
		wavestats/Q   SquareMap_dis
		A2DImageRangeMinLimit=V_min
		A2DImageRangeMin=V_min
		A2DImageRangeMaxLimit=V_max
		A2DImageRangeMax=V_max
		DoWindow SquareMapIntvsPixels
		if(!V_Flag)
			Execute ("EGN_SquareGraph()")
		else
			DoWindow/F SquareMapIntvsPixels
		endif	
		Slider ImageRangeMinSquare,win=SquareMapIntvsPixels,variable= root:Packages:Convert2Dto1D:A2DImageRangeMin,live= 0,side= 3,vert= 0,ticks= 0
		Slider ImageRangeMinSquare,limits={A2DImageRangeMinLimit,A2DImageRangeMaxLimit,0}
		Slider ImageRangeMaxSquare,win=SquareMapIntvsPixels,variable= root:Packages:Convert2Dto1D:A2DImageRangeMax,live= 0,side= 3,vert= 0,ticks= 0
		Slider ImageRangeMaxSquare,limits={A2DImageRangeMinLimit,A2DImageRangeMaxLimit,0}
	endif
	
	
end

Function EGN_SquareButtonProc(ctrlName) : ButtonControl
	String ctrlName
	
	
	if(cmpstr(ctrlName,"SaveCurrentLineout")==0)
		Wave profile=root:Packages:EGN_ImProcess:LineProfile:profile
		
		string OldDf=GetDataFolder(1)
		string NewFldrName
		//DoAlert 0, "Need to finish EGN_SquareButtonProc procedure in EGN_SquareMatrix.ipf" 
		//need to convert data into Int vs Q and then save data somewhere...
		SVAR FileNameToLoad=root:Packages:Convert2Dto1D:FileNameToLoad
		NVAR DisplayPixles=root:Packages:EGN_ImProcess:LineProfile:DisplayPixles
		NVAR DisplayQvec=root:Packages:EGN_ImProcess:LineProfile:DisplayQvec
		NVAR DisplaydSpacing=root:Packages:EGN_ImProcess:LineProfile:DisplaydSpacing
		NVAR DisplayTwoTheta=root:Packages:EGN_ImProcess:LineProfile:DisplayTwoTheta
		NVAR A2DLineoutDisplayLogInt=root:Packages:Convert2Dto1D:A2DLineoutDisplayLogInt
		wave profile=root:Packages:EGN_ImProcess:LineProfile:profile
		wave qvector=root:Packages:EGN_ImProcess:LineProfile:qvector
		wave TwoTheta=root:Packages:EGN_ImProcess:LineProfile:TwoTheta
		wave Dspacing=root:Packages:EGN_ImProcess:LineProfile:Dspacing
		wave chiangle=root:Packages:EGN_ImProcess:LineProfile:chiangle
		NVAR width=root:Packages:EGN_ImProcess:LineProfile:width
		NVAR position=root:Packages:EGN_ImProcess:LineProfile:position
		NewFldrName = CleanupName(FileNameToLoad,0)[0,20] +"_"+num2str(floor(position))+"_"+num2str(floor(width))
		Prompt NewFldrName, "Input folder name for data to be stored to"
		DoPrompt "User input", NewFldrName
		if(V_Flag)
			abort
		endif
		NewFldrName=cleanupName(NewFldrName,0)
		NewDataFolder/O/S root:SAS
		if(DataFolderExists(NewFldrName))
			DoAlert 1, "The folder with data exists, ovewrite?"
			if(V_Flag==2)
				abort
			endif
		endif
		NewDataFolder/O/S $(NewFldrName)

		Duplicate/O profile, $("r_"+NewFldrName),$("s_"+NewFldrName) 
		Wave Intensity = $("r_"+NewFldrName)
		Wave Error = $("s_"+NewFldrName) 
		if(A2DLineoutDisplayLogInt)
			Intensity=10^Intensity
		endif
		Error = sqrt(Intensity)
		if(DisplayPixles)
			Duplicate/O chiangle, $("a_"+NewFldrName)
			wave avectorN=$("a_"+NewFldrName)
			//avectorN = p // eliot added this to add an index wave for angular outputs
			EG_N2G_RemoveNaNsFrom3Waves(Intensity,Error,avectorN)
		elseif(DisplayQvec)
			Duplicate/O qvector, $("q_"+NewFldrName)
			wave QvectorN=$("q_"+NewFldrName)
			EG_N2G_RemoveNaNsFrom3Waves(Intensity,Error,QvectorN)
		elseif(DisplaydSpacing)
			Duplicate/O Dspacing, $("d_"+NewFldrName)
			wave DspacingN=$("d_"+NewFldrName)
			EG_N2G_RemoveNaNsFrom3Waves(Intensity,Error,DspacingN)
		elseif(DisplayTwoTheta)
			Duplicate/O TwoTheta, $("t_"+NewFldrName)
			wave TwoThetaN=$("t_"+NewFldrName)
			EG_N2G_RemoveNaNsFrom3Waves(Intensity,Error,TwoThetaN)
		endif		
		setDataFolder OldDf
		
	endif
End

Function EGNA_SQCCDImageUpdateColors(updateRanges)
	variable updateRanges
	
	string oldDf=GetDataFOlder(1)
	setDataFolder root:Packages:Convert2Dto1D
	NVAR ImageRangeMin= root:Packages:Convert2Dto1D:A2DImageRangeMin
	NVAR ImageRangeMax = root:Packages:Convert2Dto1D:A2DImageRangeMax
	SVAR ColorTableName=root:Packages:Convert2Dto1D:ColorTableName
	NVAR ImageRangeMinLimit= root:Packages:Convert2Dto1D:A2DImageRangeMinLimit
	NVAR ImageRangeMaxLimit = root:Packages:Convert2Dto1D:A2DImageRangeMaxLimit
//	String s= ImageNameList("", ";")
//	Variable p1= StrSearch(s,";",0)
//	if( p1<0 )
//		abort			// no image in top graph
//	endif
//	s= s[0,p1-1]
	if(updateRanges)
		Wave waveToDisplayDis=root:Packages:Convert2Dto1D:SquareMap_dis
		wavestats/Q  waveToDisplayDis
		ImageRangeMin=V_min
		ImageRangeMinLimit=V_min
		ImageRangeMax=V_max
		ImageRangeMaxLimit=V_max
		Slider ImageRangeMinSquare,limits={ImageRangeMinLimit,ImageRangeMaxLimit,0}, win=SquareMapIntvsPixels
		Slider ImageRangeMaxSquare,limits={ImageRangeMinLimit,ImageRangeMaxLimit,0}, win=SquareMapIntvsPixels
	endif
	ModifyImage/W=SquareMapIntvsPixels SquareMap_dis  ctab= {ImageRangeMin,ImageRangeMax,$ColorTableName,0}
	setDataFolder OldDf
end
